# Tests for the map::slippy module.                           -*- tcl -*-
#
# This file contains a collection of tests for one or more of the Tcl built-in commands.  Sourcing
# this file into Tcl runs the tests and generates output for errors.  No output means no errors were
# found.
#
# Copyright (c) 2010-2022 Andreas Kupries
# All rights reserved.

# -------------------------------------------------------------------------

::tcltest::testConstraint map_slippy_critcl [string equal $impl critcl]

# -------------------------------------------------------------------------

# TODO: tile valid, fit geobox

test map-slippy-${impl}-1.0 {tiles} -body {
    map slippy tiles 8
} -result 256

test map-slippy-${impl}-2.0 {length} -body {
    map slippy length 4
} -result 4096

test map-slippy-${impl}-3.0 {tile size} -body {
    map slippy tile size
} -result 256

# -------------------------------------------------------------------------
# Encoding tests
# -------------------------------------------------------------------------

test map-slippy-${impl}-7.0 {tile 2geo} -body {
    map slippy tile 2geo {11 504 775}
} -match 4digits -result {11.0 67.0674 -43.7695}

# -------------------------------------------------------------------------
# Converting between the three coordinate systems.
# -------------------------------------------------------------------------

foreach {n tile point geo tilei} {
    0  {0 0 0}     {0 0 0}     {0  85.0511287798 -180.0} {0 0 0}
    1  {0 1 1}     {0 256 256} {0 -85.0511287798  180.0} {0 0 1}
    2  {0 0.5 0.5} {0 128 128} {0 0 0}                   {0 0 0}
    3  {1 0 0}     {1 0 0}     {1  85.0511287798 -180.0} {1 0 0}
    4  {1 1 1}     {1 256 256} {1 0 0}                   {1 1 1}
    5  {1 0.5 0.5} {1 128 128} {1  66.5132604431  -90.0} {1 0 0}
    6  {1 2 2}     {1 512 512} {1 -85.0511287798  180.0} {1 1 2}
    7  {1 1.5 1.5} {1 384 384} {1 -66.5132604431   90.0} {1 1 1}
} {
    # The tile/int results for .1/.6 are
    # |0 0 1| instead of |0 1 1|
    # |1 1 2|            |1 2 2|
    # due to round off. As a float is it shown as 1.0, internally it is actually 0.9999...

    test map-slippy-${impl}-8.$n {tile -> point} -body {
	map slippy tile 2point $tile
    } -match 4digits -result $point

    test map-slippy-${impl}-9.$n {point -> tile} -body {
	map slippy point 2tile $point
    } -match 4digits -result $tile

    test map-slippy-${impl}-10.$n {point -> tile -> point} -body {
	map slippy tile 2point [map slippy point 2tile $point]
    } -match 4digits -result $point

    test map-slippy-${impl}-11.$n {tile -> point -> tile} -body {
	map slippy point 2tile [map slippy tile 2point $tile]
    } -match 4digits -result $tile

    test map-slippy-${impl}-12.$n {tile -> geo} -body {
	map slippy tile 2geo $tile
    } -match 4digits -result $geo

    test map-slippy-${impl}-13.$n {geo -> tile/float} -body {
	map slippy geo 2tile.float $geo
    } -match 4digits -result $tile

    test map-slippy-${impl}-13a.$n {geo -> tile} -body {
	map slippy geo 2tile $geo
    } -match 4digits -result $tilei

    test map-slippy-${impl}-14.$n {geo -> tile/float -> geo} -body {
	map slippy tile 2geo [map slippy geo 2tile.float $geo]
    } -match 4digits -result $geo

    test map-slippy-${impl}-15.$n {tile/float -> geo -> tile/float} -body {
	map slippy geo 2tile.float [map slippy tile 2geo $tile]
    } -match 4digits -result $tile

    test map-slippy-${impl}-16.$n {point -> geo} -body {
	map slippy point 2geo $point
    } -match 4digits -result $geo

    test map-slippy-${impl}-17.$n {geo -> point} -body {
	map slippy geo 2point $geo
    } -match 4digits -result $point

    test map-slippy-${impl}-18.$n {geo -> point -> geo} -body {
	map slippy point 2geo [map slippy geo 2point $geo]
    } -match 4digits -result $geo

    test map-slippy-${impl}-19.$n {point -> geo -> point} -body {
	map slippy geo 2point [map slippy point 2geo $point]
    } -match 4digits -result $point
}

foreach {n geo points} {
    0 {0 85.0511287798 -180.0} {
        {0 0 0}
        {1 0 0}
        {2 0 0}
        {3 0 0}
        {4 0 0}
    }
    1 {0 -85.0511287798 180.0} {
        {0  256  256}
        {1  512  512}
        {2 1024 1024}
        {3 2048 2048}
        {4 4096 4096}
    }
    2 {0 0 0} {
        {0  128  128}
        {1  256  256}
        {2  512  512}
        {3 1024 1024}
        {4 2048 2048}
    }
    3 {0 66.5132604431 -90.0} {
        {0   64   64}
        {1  128  128}
        {2  256  256}
        {3  512  512}
        {4 1024 1024}
    }
    4 {0 -66.5132604431 90.0} {
        {0  192  192}
        {1  384  384}
        {2  768  768}
        {3 1536 1536}
        {4 3072 3072}
    }
} {
    test map-slippy-${impl}-20.$n {geo -> points (at all levels)} -body {
	map slippy geo 2points 5 $geo
    } -match x4digits -result $points
}

# -------------------------------------------------------------------------
# Local variables:
# mode: tcl
# indent-tabs-mode: nil
# End:
